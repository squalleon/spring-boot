plugins{
    id 'java'
    id 'org.springframework.boot' version '1.5.8.RELEASE'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    jcenter()
}

sourceSets{
    main {
        java.srcDir "src/main/java"
        resources.srcDir "src/main/resources"
    }

    test{
        java.srcDir "src/test/java"
        resources.srcDir "src/test/resources"
    }

    integrationTest {
        java.srcDir "src/integrationTest/java"
        resources.srcDir "src/integrationTest/resources"

        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

task integrationTest(type: Test) {
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }
}

check.dependsOn integrationTest
integrationTest.mustRunAfter test

tasks.withType(Test) {
    reports.html.destination = file("${reporting.baseDir}/${name}")
}


dependencies {
    compile 'org.springframework.boot:spring-boot-starter-web'
    compile  'org.springframework.boot:spring-boot-starter-tomcat'
    //db
    compile  group: 'com.h2database', name: 'h2', version: '1.4.193'
    compile('org.mybatis.spring.boot:mybatis-spring-boot-starter:1.3.0')
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-aop'
    testCompile("org.springframework.boot:spring-boot-starter-test")
}
